buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:5.+'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = "1.12.2-1.11.1"
group = "hellfirepvp.modularmachinery"
archivesBaseName = "modularmachinery"
String build_version = ""

// Most likely inside Jenkins. Or homebrew environment.
// I hope you know what you do if it's the latter.
if (System.getenv("BUILD_NUMBER") != null) {
    build_version = version + "-v" + System.getenv("BUILD_NUMBER")
}

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {

    mappings channel: 'snapshot', version: '20171003-1.12'

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'
        }

        server {

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'
        }
    }
}

repositories {
    maven {
        // for rf stuffs
        url = "https://maven.covers1624.net"
    }

    maven {
        // location of the maven that hosts JEI files
        url = "http://dvs1.progwml6.com/files/maven"
    }

    maven {
        // CraftTweaker
        url "https://maven.blamejared.com/"
    }

    maven {
        // Brandonscore and DE
        url "http://chickenbones.net/maven/"
    }

    maven {
        // Open Computers
        url "http://maven.cil.li/"
    }

    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

}

dependencies {

    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2855'

    compileOnly fg.deobf('mezz.jei:jei_1.12.2:4.14.4.267')
    runtimeOnly fg.deobf('mezz.jei:jei_1.12.2:4.14.4.267')
    implementation 'CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.+'
    
    implementation fg.deobf("curse.maven:RedstoneFlux-270789:2920436")
    implementation "curse.maven:CodeChickenLib-242818:2779848"
    implementation "curse.maven:brandonscore-231382:3051539"
    implementation "curse.maven:draconicevolution-223565:3051542"
    implementation "curse.maven:gregtech-293327:3266351"
    implementation "curse.maven:Mekanism-268560:2835175"
    implementation "curse.maven:industrialcraft-2-242638:3078604"
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

//Sign jar from flatfile if present
task signJar(type: SignJar, dependsOn: jar) {
    onlyIf {
        project.hasProperty('keyStore')
    }
    if (project.hasProperty('keyStore')) {
        keyStore = project.keyStore
        alias = project.alias
        storePass = project.storePass
        keyPass = project.keyPass
        inputFile = jar.archivePath
        outputFile = jar.archivePath
    } else {
        logger.warn("No key store found, not signing the output jar\n")
    }
}

build.dependsOn signJar

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
                "Specification-Title": "astralsorcery",
                "Specification-Vendor": "hellfirepvp",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" : "hellfirepvp",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task deobfJar(type: Jar) {
    from(sourceSets.main.output)
    archiveName = "${baseName}-${version}-deobf.${extension}"
}

task srcJar(type: Jar) {
    from(sourceSets.main.java)
    classifier = 'sources'
    archiveName = "${baseName}-${version}-sources.${extension}"
}

artifacts {
    archives deobfJar, srcJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version build_version
            from components.java

            artifact deobfJar {
                classifier 'deobf'
            }
            artifact srcJar {
                classifier 'sources'
            }
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("LOCAL_MAVEN")
        }
    }
}
